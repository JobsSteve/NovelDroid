import groovy.swing.SwingBuilder

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.sh1r0.noveldroid"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 53
        versionName "1.5.0"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:preference-v7:23.3.0'
    compile 'com.android.support:preference-v14:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.0'
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.github.medyo:android-about-page:1.0.7'
}

def ksFile = rootProject.file('local.properties')
def props = new Properties();
if (ksFile.canRead()) {
    props.load(new FileInputStream(ksFile))

    if (props != null) {
        android.signingConfigs.release.storeFile = file(props['KEYSTORE_FILE'])
        android.signingConfigs.release.storePassword = props['KEYSTORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEYSTORE_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'Some entries in \'local.properties\' not found!'
    }
} else {
    println '\'local.properties\' not found!'
}
